
apply plugin: 'es.hadoop.build.integration'

description = "Elasticsearch Hadoop Hive"

evaluationDependsOn(':elasticsearch-hadoop-mr')

dependencies {
    provided(project(":elasticsearch-hadoop-mr"))
    provided(project(path: ":elasticsearch-hadoop-mr", configuration:"compile"))

    testCompile project(":elasticsearch-hadoop-mr").sourceSets.test.runtimeClasspath
    itestCompile project(":elasticsearch-hadoop-mr").sourceSets.itest.runtimeClasspath
}

jar {
    from(zipTree(project(":elasticsearch-hadoop-mr").jar.archivePath)) {
        include "org/elasticsearch/hadoop/**"
        include "esh-build.properties"
        include "META-INF/services/*"
    }
}

javadoc {
    source += project(":elasticsearch-hadoop-mr").sourceSets.main.allJava
    classpath += files(project(":elasticsearch-hadoop-mr").sourceSets.main.compileClasspath)
}

sourcesJar {
    from project(":elasticsearch-hadoop-mr").sourceSets.main.allJava.srcDirs
}

dependencies {
    provided("org.apache.hive:hive-service:$hiveVersion") {
        exclude module: "log4j-slf4j-impl"
    }
    provided("org.apache.hive:hive-exec:$hiveVersion")
    provided("org.apache.hive:hive-metastore:$hiveVersion")

    itestRuntime("org.apache.hive:hive-service:$hiveVersion") {
        exclude module: "log4j-slf4j-impl"
    }
    itestRuntime("org.apache.hive:hive-jdbc:$hiveVersion") {
        exclude module: "log4j-slf4j-impl"
    }
}
