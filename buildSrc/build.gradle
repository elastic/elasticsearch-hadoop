import java.nio.file.Files

apply plugin: 'groovy'

dependencies {
    compile gradleApi()
    compile localGroovy()
}

/*
 * Configure Project Versions
 */

Properties props = new Properties()
props.load(project.file('version.properties').newDataInputStream())
version = props.getProperty('eshadoop')

// determine if we're building a snapshot or not (by default we will be)
boolean snapshot = "true".equals(System.getProperty("build.snapshot", "true"))
if (snapshot) {
    // we update the version property to reflect if we are building a snapshot or a release build
    version += "-SNAPSHOT"
    props.put("eshadoop", version)
}

// write the updated properties to a temp property file
File tempPropertiesFile = new File(project.buildDir, "version.properties")
task writeVersionProperties {
    inputs.properties(props)
    outputs.file(tempPropertiesFile)
    doLast {
        OutputStream stream = Files.newOutputStream(tempPropertiesFile.toPath())
        try {
            props.store(stream, "UTF-8")
        } finally {
            stream.close()
        }
    }
}

// copy the saved property file to the resources dir
processResources {
    dependsOn writeVersionProperties
    from tempPropertiesFile
}