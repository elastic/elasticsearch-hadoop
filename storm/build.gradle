
apply plugin: 'es.hadoop.build.integration'

description = "Elasticsearch Storm"

evaluationDependsOn(':elasticsearch-hadoop-mr')

dependencies {
    provided(project(":elasticsearch-hadoop-mr"))
    provided(project(path: ":elasticsearch-hadoop-mr", configuration:"compile"))

    testCompile project(":elasticsearch-hadoop-mr").sourceSets.test.runtimeClasspath
    itestCompile project(":elasticsearch-hadoop-mr").sourceSets.itest.runtimeClasspath
}

jar {
    from(zipTree(project(":elasticsearch-hadoop-mr").jar.archivePath)) {
        include "org/elasticsearch/hadoop/**"
        include "esh-build.properties"
        include "META-INF/services/*"
    }
}

javadoc {
    source += project(":elasticsearch-hadoop-mr").sourceSets.main.allJava
    classpath += files(project(":elasticsearch-hadoop-mr").sourceSets.main.compileClasspath)
}

sourcesJar {
    from project(":elasticsearch-hadoop-mr").sourceSets.main.allJava.srcDirs
}

dependencies {
    provided("org.apache.storm:storm-core:$stormVersion") {
        exclude module: "log4j-slf4j-impl"
    }

    itestCompile "com.google.guava:guava:16.0.1"
    itestRuntime "com.twitter:carbonite:1.4.0"
}

// add itest to Eclipse
//eclipse.classpath.plusConfigurations += [configurations.itestCompile]

tasks.getByName('integrationTest').enabled = false
