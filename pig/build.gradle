
apply plugin: 'es.hadoop.build.integration'

description = "Elasticsearch Hadoop Pig"

evaluationDependsOn(':elasticsearch-hadoop-mr')

configurations {
    embedded {
        transitive = false
        canBeResolved = true
    }
    implementation {
        extendsFrom project.configurations.embedded
    }
    itestEmbedded {
        transitive = false
        canBeResolved = true
    }
    itestImplementation {
        extendsFrom project.configurations.itestEmbedded
    }
}

ext.pigClassifier = "h2"

dependencies {
    embedded(project(":elasticsearch-hadoop-mr"))

    implementation("org.apache.pig:pig:$pigVersion:$pigClassifier")
    implementation("joda-time:joda-time:$jodaVersion")
    implementation("commons-logging:commons-logging:1.1.1")
    implementation("javax.xml.bind:jaxb-api:2.3.1")

    testImplementation(project(":test:shared"))
    testImplementation("com.fasterxml.jackson.core:jackson-databind:2.7.8")
    testImplementation("org.apache.pig:pig:$pigVersion:$pigClassifier")
    testImplementation("joda-time:joda-time:$jodaVersion")
    testImplementation("com.google.guava:guava:11.0")
    testImplementation("jline:jline:0.9.94")

    itestEmbedded(project(":test:shared"))
    itestImplementation("com.fasterxml.jackson.core:jackson-databind:2.7.8")
    itestImplementation("dk.brics.automaton:automaton:1.11-8")

    additionalSources(project(":elasticsearch-hadoop-mr"))
    javadocSources(project(":elasticsearch-hadoop-mr"))
}

tasks.named("test").configure {
    onlyIf { JavaVersion.current() >= JavaVersion.VERSION_16 }
    jvmArgs "--add-opens=java.base/java.io=ALL-UNNAMED" // Needed for IOUtils's BYTE_ARRAY_BUFFER reflection
}

jar {
    dependsOn(project.configurations.embedded)
    from(project.configurations.embedded.collect { it.isDirectory() ? it : zipTree(it)}) {
        include "org/elasticsearch/hadoop/**"
        include "esh-build.properties"
        include "META-INF/services/*"
    }
}

itestJar {
    dependsOn(project.configurations.embedded)
    dependsOn(project.configurations.itestEmbedded)
    from(project.configurations.embedded.collect { it.isDirectory() ? it : zipTree(it)}) {
        include "org/elasticsearch/hadoop/**"
        include "esh-build.properties"
        include "META-INF/services/*"
    }
    from(project.configurations.itestEmbedded.collect { it.isDirectory() ? it : zipTree(it)}) {
        include "org/elasticsearch/hadoop/**"
        include "esh-build.properties"
        include "META-INF/services/*"
    }
}
