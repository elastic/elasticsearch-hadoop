
apply plugin: 'es.hadoop.build.integration'

description = "Elasticsearch Hadoop Map/Reduce"

dependencies {
    provided(project.ext.hadoopClient)
    provided("org.codehaus.jackson:jackson-mapper-asl:${project.ext.jacksonVersion}")
    testCompile "io.netty:netty-all:4.0.29.Final"
    testCompile "org.elasticsearch:securemock:1.2"
}

String generatedResources = "$buildDir/generated-resources/main"

sourceSets {
    main {
        output.dir(generatedResources, builtBy: "generateGitHash")
    }
}

task generateGitHash {
    def buildPropsFile = new File(generatedResources, "esh-build.properties")
    
    inputs.file project.ext.gitHead
    outputs.file buildPropsFile

    doLast {
        Properties props = new Properties()
        props.put("version", version)
        props.put("hash", project.ext.revHash)
        new File(generatedResources).mkdirs()
        buildPropsFile.createNewFile()
        props.store(buildPropsFile.newWriter(), null)
    }
}

eclipse.classpath.file {
    whenMerged { cp ->
        // export all jars (to be used upstream by dependent projects)  <-- for some reason Gradle removes all jars
        cp.entries.each { entry ->
            if (entry.hasProperty("exported"))
                entry.exported = true
        }
    }
}
