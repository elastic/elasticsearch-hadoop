apply plugin: "maven"
apply plugin: "propdeps-maven"

def customizePom(pom, gradleProject) {
    pom.whenConfigured { generatedPom ->

        // eliminate test-scoped dependencies (no need in maven central poms)
        generatedPom.dependencies.removeAll { dep ->
            dep.scope == 'test' || dep.artifactId == 'elasticsearch-hadoop-mr'
        }
		
		// for es-hadoop optional is best served as provided/optional vs compile/optional
		generatedPom.dependencies.findAll { it.optional == true }.each {
			it.scope = "provided"
		}

        // add all items necessary for maven central publication
        generatedPom.project {
            name = gradleProject.description
            description = gradleProject.description
            url = 'http://github.com/elasticsearch/elasticsearch-hadoop'
            organization {
                name = 'Elasticsearch'
                url = 'http://www.elasticsearch.org/'
            }
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            scm {
                url = 'http://github.com/elasticsearch/elasticsearch-hadoop'
                connection = 'scm:git:git://github.com/elasticsearch/elasticsearch-hadoop'
                developerConnection = 'scm:git:git://github.com/elasticsearch/elasticsearch-hadoop'
            }
            developers {
                developer {
                    id = 'costin'
                    name = 'Costin Leau'
                    email = 'costin@elasticsearch.com'
                    properties {
                        twitter = 'costinl'
                    }
                }
            }
        }
		
		groupId = "org.elasticsearch"
		artifactId = project.name
	}
}

ext.deployUsername = { project.hasProperty("repoUsername") ? getProperty("repoUsername") : "" }
ext.deployPassword = { project.hasProperty("repoPassword") ? getProperty("repoPassword") : "" }

repositories {
	flatDir {
		name "fileRepo"
		dirs "repo"
	}
}

uploadArchives {
	repositories {
		//add project.repositories.fileRepo
		mavenDeployer {
			customizePom(pom, project)
			
			repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
				authentication(userName: deployUsername(), password: deployPassword())
            }
			snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
				authentication(userName: deployUsername(), password: deployPassword())
			}
		}
	}
}

//evaluationDependsOn "elasticsearch-hadoop-repository-hdfs"
//install.dependsOn subprojects:assemble

install {
	repositories.mavenInstaller {
		customizePom(pom, project)
	}
}