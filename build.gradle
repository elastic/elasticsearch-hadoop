
description = 'Elasticsearch for Apache Hadoop'

apply plugin: 'es.hadoop.build.root'

defaultTasks 'build'

build {
    dependsOn 'hadoopTestingJar'
}

task wrapper(type: Wrapper) {
    description = 'Generates gradlew[.bat] scripts'
    gradleVersion = '4.5'
}

// Simple utility task to help with downloading artifacts and jars
if (project.hasProperty("find-artifact")) {
    String artifact = project.getProperty("find-artifact")

    configurations {
        findJar
    }

    dependencies {
        findJar artifact
    }

    task find(type: Copy) {
        dependsOn configurations.findJar
        from configurations.findJar.first()
        into new File(rootProject.buildDir, 'found/')
        doLast {
            if (configurations.findJar.files.size() > 0) {
                String artifactName = configurations.findJar.first().name
                File found = new File(rootProject.buildDir, 'found/')
                logger.lifecycle("Found [$artifactName] and stored it in [$found]")
                if (configurations.findJar.size() > 1) {
                    configurations.findJar.each { logger.lifecycle("Also found other dependencies: [${ it.name }]") }
                }
            } else {
                logger.lifecycle("Could not find artifact [$artifact]")
            }
        }
    }
}
