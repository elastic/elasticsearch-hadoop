buildscript {
    repositories {
        maven { url 'http://repo.springsource.org/plugins-release' }
    }
    dependencies {
        classpath 'org.springframework.build.gradle:propdeps-plugin:0.0.3'
    }
}

description = 'ElasticSearch Hadoop'
group = 'org.elasticsearch.hadoop'

repositories {
  mavenCentral()
  maven { url "http://oss.sonatype.org/content/groups/public/" }
  // JDO ec2 missing from Maven Central
  maven { url "http://www.datanucleus.org/downloads/maven2" }
  maven { url "http://conjars.org/repo" }
}

apply plugin: "java"
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'propdeps'
apply plugin: 'propdeps-idea'
apply plugin: 'propdeps-eclipse'

def antlrVersion

dependencies {
	ext.hadoopVersion=hadoop10Version
	
    compile("org.apache.hadoop:hadoop-streaming:$hadoopVersion")
	compile("org.apache.hadoop:hadoop-tools:$hadoopVersion")
	
	optional("commons-io:commons-io:2.1")
	compile("org.codehaus.jackson:jackson-mapper-asl:1.8.8")
    testRuntime("log4j:log4j:$log4jVersion")

    // Pig
    optional("org.apache.pig:pig:$pigVersion")

	ext.hiveGroup = "org.apache.hive"
    // Hive
    optional("$hiveGroup:hive-service:$hiveVersion")
	
	// Cascading
	optional("cascading:cascading-hadoop:$cascadingVersion")
	optional("cascading:cascading-local:$cascadingVersion")
    
    // needed by the Hive Server tests
    testRuntime "$hiveGroup:hive-builtins:$hiveVersion" 

    // Testing
    testCompile "junit:junit:$junitVersion"
	testCompile "org.hamcrest:hamcrest-all:$hamcrestVersion"
    testCompile "org.elasticsearch:elasticsearch:$esVersion"
	
	// Required by Pig
	testRuntime "com.google.guava:guava:11.0"
	testRuntime "dk.brics.automaton:automaton:1.11-8"
}

configurations.all {
  resolutionStrategy {
	forcedModules = ['commons-httpclient:commons-httpclient:3.0.1']
  }
}

sourceCompatibility = 1.6
targetCompatibility = 1.6 

ext.skipCascading = true
ext.skipHive = true
ext.skipPig = true

task enableCascadingTests {
    description = "Enable Cascading tests"
    group = "Verification"
    doLast() {
        project.ext.skipCascading = false
   }
}

task enableHiveTests {
    description = "Enable Hive tests"
    group = "Verification"
    doLast() {
        project.ext.skipHive = false
		project.ext.skipPig = true
		antlrVersion = antlrForHiveVersion
		dependencies {
			testRuntime "org.antlr:antlr-runtime:$antlrForHiveVersion"
		}
   }
}

task enablePigTests {
    description = "Enable Pig tests"
    group = "Verification"
    
    doLast() {
        project.ext.skipPig = false
		project.ext.skipHive = true
		dependencies {
			testRuntime "org.antlr:antlr-runtime:$antlrForPigVersion"
		}
  }
}

task enableIntegrationTests() {
    description = "Enable all integration tests"
    group = "Verification"
    doLast() {
      println "Enable all integration tests"
  	  project.ext.skipCascading = false
	  enablePigTests.execute()
    }
}

test {
    systemProperties['input.path'] = 'build/classes/test/input'
    systemProperties['output.path'] = 'build/classes/test/output'
    includes = ["org/elasticsearch/hadoop/unit/**/*.class", "org/elasticsearch/hadoop/integration/**/*Suite.class"]

    testLogging {
        events "started" //, "standardOut", "standardError"
        minGranularity 2
        maxGranularity 2
    }

    doFirst() {
        ext.msg = ""
        
        if (skipCascading) {
            ext.msg += "Cascading "
            excludes.add("**/cascading/**")
        }
        if (skipHive) {
            ext.msg += "Hive "
            excludes.add("**/hive/**")
        }
        if (skipPig) {
            ext.msg += "Pig"
            excludes.add("**/pig/**")
        }

        if (!msg.isEmpty())
            println "Skipping [$msg] Tests";
	}
}

task sourcesJar(type: Jar, dependsOn:classes) {
    classifier = 'sources'
    from sourceSets.main.allJava
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}


jar {
    manifest.attributes["Created-By"] = "${System.getProperty("java.version")} (${System.getProperty("java.specification.vendor")})"
    manifest.attributes['Implementation-Title'] = 'elasticsearch-hadoop'
    manifest.attributes['Implementation-Version'] = project.version
    manifest.attributes['Implementation-URL'] = "http://github.com/elasticsearch/elasticsearch-hadoop"
    manifest.attributes['Implementation-Vendor'] = "Elasticsearch"
    manifest.attributes['Implementation-Vendor-Id'] = "org.elasticsearch"
    
    def build = System.env['ESHDP.BUILD']
    if (build != null)
        manifest.attributes['Build'] = build
    
    String rev = "unknown"
    
    // parse the git files to find out the revision
    File gitHead = file('.git/HEAD')
    if (gitHead.exists()) {
        gitHead = file('.git/' + gitHead.text.trim().replace('ref: ',''))
        if (gitHead.exists()) { rev = gitHead.text }
    }

    from("$rootDir/docs/src/info") {
        include "license.txt"
        include "notice.txt"
        into "META-INF"
        expand(copyright: new Date().format('yyyy'), version: project.version)
    }

    manifest.attributes['Repository-Revision'] = rev
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

task wrapper(type: Wrapper) {
    description = 'Generates gradlew[.bat] scripts'
    gradleVersion = '1.4'
}

assemble.dependsOn = ['jar', 'sourcesJar']
defaultTasks 'build'